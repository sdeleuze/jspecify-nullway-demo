plugins {
    id 'java'
    id 'net.ltgt.errorprone' version '4.1.0' // https://github.com/tbroyer/gradle-errorprone-plugin
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

dependencies {
    implementation 'org.jspecify:jspecify:1.0.0' // https://jspecify.org
    errorprone 'com.uber.nullaway:nullaway:0.12.6' // https://github.com/uber/NullAway
    errorprone 'com.google.errorprone:error_prone_core:2.37.0' // https://github.com/google/error-prone
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableAllChecks = true // Other error prone checks are disabled
        option("NullAway:OnlyNullMarked", "true") // Enable nullness checks only in null-marked code
        error("NullAway") // bump checks from warnings (default) to errors
        option("NullAway:JSpecifyMode", "true") // https://github.com/uber/NullAway/wiki/JSpecify-Support
        option("NullAway:JSpecifyMode", "true") // https://github.com/uber/NullAway/wiki/JSpecify-Support
    }
}
